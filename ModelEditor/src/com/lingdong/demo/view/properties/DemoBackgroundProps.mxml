<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009" 
					xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx" 
					width="100%" height="100%" label="编辑背景" show="updateProperties()">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.lingdong.demo.model.events.DemoBackgroundEvent;
			import com.lingdong.demo.model.resources.DemoBackground;
			
			private var _background:DemoBackground;
			
			public function get background():DemoBackground
			{
				return _background;
			}
			
			public function set background(value:DemoBackground):void
			{
				if (_background != value)
				{
					_background && _background.removeEventListener(DemoBackgroundEvent.COLOR_CHANGE, updateColor);
					_background && _background.removeEventListener(DemoBackgroundEvent.COMMENT_CHANGE, updateComment);
					
					_background = value;
					
					updateProperties();
					
					_background && _background.addEventListener(DemoBackgroundEvent.COLOR_CHANGE, updateColor);
					_background && _background.addEventListener(DemoBackgroundEvent.COMMENT_CHANGE, updateComment);
				}
			}
			
			private function updateProperties():void
			{
				updateColor();
				updateComment();
			}
			
			private function updateColor(event:Event = null):void
			{
				if (this.colorPicker)
				{
					colorPicker.selectedColor = this.background ? this.background.color : 0x000000;
				}
			}
			
			private function updateComment(event:Event = null):void
			{
				if (this.textArea)
				{
					textArea.text = this.background ? this.background.comment : "";
				}
			}
			
			private function changeColor():void
			{
				if (this.background)
				{
					this.background.url = null;
					this.background.color = colorPicker.selectedColor;
				}
			}
			
			private function changeComment():void
			{
				if (this.background)
				{
					this.background.comment = textArea.text;
				}
			}
		]]>
	</fx:Script>
	
	<mx:Form width="100%" height="100%" paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5">
		<mx:FormItem width="100%" label="背景色:">
			<mx:ColorPicker id="colorPicker" change="changeColor()" />
		</mx:FormItem>

		<mx:FormItem width="100%" label="解说词:" />
		<s:TextArea width="100%" height="100%" id="textArea" change="changeComment()" />
	</mx:Form>
</s:NavigatorContent>
